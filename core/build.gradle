import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.dokka.DokkaConfiguration.Visibility

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "com.vanniktech.maven.publish"
    id "org.jetbrains.dokka"
}

android {
    namespace 'pers.shawxingkwok.androidutil'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    // TODO
    api 'io.github.shawxingkwok:kt-util:1.0.0-SNAPSHOT'
    //noinspection GradleDependency
    api "org.jetbrains.kotlin:kotlin-reflect:$version_kt"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.withType(KotlinCompile).configureEach{
    kotlinOptions.freeCompilerArgs += "-Xexplicit-api=warning"
//    kotlinOptions.freeCompilerArgs += "-Xcontext-receivers"
}

// dokka
tasks.withType(DokkaTask.class) {
    suppressInheritedMembers.set(true)

    outputDirectory.set(file("/Users/william/IdeaProjects/library/ITWorks/content/docs/androidutil/html"))

    dokkaSourceSets.configureEach {
        documentedVisibilities.set([
                Visibility.PUBLIC,
                Visibility.PROTECTED,
        ])

        perPackageOption {
            matchingRegex.set(".*internal.*")
            suppress.set(true)
        }
    }
}

// publish
mavenPublishing {
    coordinates("io.github.shawxingkwok", "android-util", "1.0.0-SNAPSHOT")
    pom {
        name = "Personal andorid util"
        description = "Contains only a log util at present."
        inceptionYear = "2023"
    }
}